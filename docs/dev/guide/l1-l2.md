# L1 -> L2 communication

This section describes the interface for interaction with zkSync from L1. It assumes that you are already familiar with the basic concepts of working with the priority queue. You can read the conceptual introduction [here](../zksync-v2/l1-l2-interop.md#priority-queue). If you are new to this topic. You can read the cross-chain governance [tutorial](./cross-chain-tutorial.md) to dive straight into the code.

## Structure

Besides the input parameters of the operations, all requests should contain the last parameter - the `_queueType`. These are the important parts of the censorship-resistant protocol called [priority queue](../zksync-v2/l1-l2-interop.md#priority-queue). For the current testnet phase, you should supply this value as `0`, i.e., `QueueType.Deque`.

To prevent users from requesting too many heavy operations from the operator, users must provide the fee to be paid to the zkSync operator in ETH. Each transaction has _base cost_ in gas and the _layer 2 tip_, equal to the basic cost of the requested transaction subtracted from the ETH value passed with the call.

Basic costs are defined in gas and not ETH, so the amount of ether that the submission of the transaction will cost depends on the gas price. Generally, the flow for calling any of these methods should be the following:

1. Fetch the gas price that you will use during the transaction call.
2. Get the base cost for the transaction.
3. Call the transaction, while passing the needed `value` with the call.

Note that since transactions in `QueueType.Deque` are processed sequentially, it does not make sense to pass more than the minimal required for `value`.

## Using contract interface in your project

To interact with the zkSync the mailbox contract using Solidity, you need to use the zkSync contract interface. There are two main ways to get it:

- By importing it from the `@matterlabs/zksync-contracts` npm package. (preferred)
- By downloading the contracts from the [repo](https://github.com/matter-labs/v2-testnet-contracts).

The `@matterlabs/zksync-contracts` package can be installed by running the following command:

```
yarn add -D @matterlabs/zksync-contracts
```

The examples below assume that the interface is accessed via the `@matterlabs/zksync-contracts` npm package.

### Getting the base cost


The following view function returns the amount of ETH needed to be supplied by the user to cover the base cost of the transaction.

```solidity
function l2TransactionBaseCost(
    uint256 _gasPrice,
    uint256 _ergsLimit,
    uint32 _calldataLength,
    QueueType _queueType
) external view returns (uint256);
```

- `_gasPrice` is a parameter that contains the transaction gas price.
- `_ergsLimit` is a parameter that contains the ergs limit of the transaction call. You can learn more about ergs and the zkSync fee system [here](../zksync-v2/fee-model).
- `_calldataLength` is a parameter that contains the length of the calldata in bytes.
- `_queueType` is a parameter required for the priority mode functionality. For the testnet, `QueueType.Deque` should always be supplied.

### Interface


The following function returns the canonical hash or the requested transaction, that you can use to track the execution of the transaction in L2.

```solidity
function requestL2Transaction(
    address _contractAddressL2,
    bytes calldata _calldata,
    uint256 _ergsLimit,
    bytes[] calldata _factoryDeps,
    QueueType _queueType
) external payable returns (bytes32 txHash);
```

- `_contractAddressL2` is a parameter that defines the address of the contract to be called.
- `_calldata` is a parameter that contains the calldata of the transaction call. It can be encoded the same way as on Ethereum.
- `_ergsLimit` is a parameter that contains the ergs limit of the transaction call. You can learn more about ergs and the zkSync fee system [here](../zksync-v2/fee-model).
- `_factoryDeps` is a list of bytecodes. It should contain the bytecode of the contract being deployed. If the contract being deployed is a factory contract, i.e. it can deploy other contracts, the array should also contain the bytecodes of the contracts that can be deployed by it.
- `_queueType` is a parameter required for the priority mode functionality. For the testnet, `QueueType.Deque` should always be supplied.


With the method call, some amount of ETH should be supplied to cover the base cost of the transaction + layer 2 operator tip.


### Examples

#### Solidity

```solidity
//SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.0;

// Importing zkSync contract interface
import "@matterlabs/zksync-contracts/l1/contracts/zksync/interfaces/IZkSync.sol";
// Importing `Operations` contract which has the `QueueType` type
import "@matterlabs/zksync-contracts/l1/contracts/zksync/Operations.sol";

contract Example {
    function callZkSync(
        // The address of the zkSync smart contract.
        // It is not recommended to hardcode it during the alpha testnet as regenesis may happen.
        address _zkSyncAddress
    ) external payable returns(bytes32 txHash) {
        IZkSync zksync = IZkSync(_zkSyncAddress);
        address someL2Contract = 0xdba0833e8c4b37cecc177a665e9207962e337299;
        // calling L2 smart contract from L1 Example contract
        txHash = zksync.requestL2Transaction{value: msg.value}(
            // The address of the L2 contract to call
            someL2Contract,
            // Encoding the calldata for the execute
            abi.encodeWithSignature("someMethod()"),
            // Ergs limit
            10000,
            // factory dependencies
            new bytes[](0),
            // The queue type
            QueueType.Deque
        );
    }
}
```

#### `zksync-web3`

```ts
import { Wallet, Provider } from "zksync-web3";
import { ethers, BigNumber } from "ethers";

const TEST_PRIVATE_KEY = "0xc8acb475bb76a4b8ee36ea4d0e516a755a17fad2e84427d5559b37b544d9ba5a";

const zkSyncProvider = new Provider("https://zksync2-testnet.zksync.dev");
const ethereumProvider = ethers.getDefaultProvider("goerli");
const wallet = new Wallet(TEST_PRIVATE_KEY, zkSyncProvider, ethereumProvider);

const gasPrice = await wallet.providerL1!.getGasPrice();

// The calldata can be encoded the same way as for Ethereum
const calldata = "0x...";
const ergsLimit = BigNumber.from(1000);

const txCostPrice = await wallet.getBaseCost({
  gasPrice,
  calldataLength: ethers.utils.hexDataLength(calldata),
  ergsLimit,
});

console.log(`Executing the transaction will cost ${ethers.utils.formatEther(txCostPrice)} ETH`);

// initiating L2 transfer via L1 execute from zksync wallet
const someL2Contract = "0x19a5bfcbe15f98aa073b9f81b58466521479df8d";
const executeTx = await wallet.requestExecute({
  calldata,
  ergsLimit,
  contractAddress: someL2Contract,
  overrides: {
    gasPrice,
  },
});

await executeTx.wait();
```
